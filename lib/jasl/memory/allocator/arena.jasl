module arena

include "memory"

pub layout ArenaAllocator {
    base: void*;
    pub capacity: u32;
    mut offset: u32;
}

//
// Public API
//
pub fn ArenaAllocator::new(capacity: u32) -> ArenaAllocator {
    let mem := memory::malloc(capacity, allocator::default());

    if mem == nullptr { return ArenaAllocator(mem, 0u32, 0u32); }
    return ArenaAllocator(mem, capacity, 0u32);
}

pub fn ArenaAllocator::allocate(this: ArenaAllocator*, size: u32) -> void* {
    let mut mem: void* = nullptr;

    if this.offset.* + size <= this.capacity.* {
        mem = this.base.*.offset(this.offset.*);
        this.offset.set(this.offset.* + size);
    }

    return mem;
}

pub fn ArenaAllocator::free(this: ArenaAllocator*) -> bool {
    let status := memory::free(this.base.*, this.capacity.*, allocator::default());

    if status {
        this.set(ArenaAllocator(nullptr, 0u32, 0u32));
    }

    return status;
}

pub fn ArenaAllocator::allocator(this: ArenaAllocator*) -> allocator::Allocator {
    return allocator::Allocator(
        this.ptrcast(void),
        allocate.ptrcast(fn (void*,u32) -> void*),
        _deallocate
    );
}

//
// Private Implementation
//
fn ArenaAllocator::_deallocate(ctx: void*, ptr: void*, size: u32) -> bool {
    return false;
}
